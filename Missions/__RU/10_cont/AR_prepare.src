export mortar_bunkers,            // seznam bunkru pro mortary
       sniper_bunkers;            // seznam bunkru pro snipery

export origin_sol,                // seznam arabskych vojaku v zakladne
       origin_def,                // seznam zabunkrovanych vojaku
       origin_others;             // seznam ostatnich osob v zakladne

export mines_list;                // seznam souradnic na kterych jsou miny

var levels;


// inicializacni funkce pro arabske jednotky
function init_variables;
begin
  mortar_bunkers:=[];
  sniper_bunkers:=[];

  origin_sol:=[];
  origin_def:=[];
  origin_others:=[];

  mortarman:=[];
  mortarman_veh:=[];
  kamikaze:=[];
  north_flamers:=[];
  north_flamers_veh:=[];
  south_flamers:=[];
  south_flamers_veh:=[];
  snipers:=[];
  snipers_veh:=[];
  hover_riders:=[];
  hovercrafts:=[];
  laser_riders:=[];
  lasers:=[];

  levels:=[4,5,7];

  // zakladni nastaveni pro tvorbu arabskych jednotek
  uc_nation:=nation_arabian;
  uc_side:=enemy;
  uc_direction:=Rand(0,5);
end;


// zpousteci funkce pro vytvoreni arabskych obranych sil
export function Ar_prepare;
begin
  init_variables;

  prepare_kamikaze;
  prepare_mortarman;
  prepare_sniper;
  prepare_hovers;
  prepare_flamers;
  prepare_lasers;
  create_minefield;

  prepare_ar_base;
end;

// vytvareni Mortarmanu
function prepare_mortarman;
var i,
    place,          // pozice pro mortary (x,y)
    breas,          // a jejich breastworky (x,y,dir)
    vehic;          // a vozidla ve kterych budou prchat
begin
  place:=[ 41,22, 63,45, 72,52];
  breas:=[ 43,25,0, 63,43,2, 71,53,0];
  vehic:=[ 39,14, 67,47, 70,49];

  uc_direction:=1;
  vc_chassis:=ar_light_trike;
  vc_control:=control_manual;
  vc_engine:=engine_combustion;
  vc_weapon:=ar_multimissile_ballista;
  for i=1 to 3 do
    begin
      PrepareHuman(sex_male,class_mortar,(-1)*(levels[difficulty]+Rand(0,1)));
      mortarman:=mortarman ^ CreateHuman;
      mortarman_veh:=mortarman_veh ^ CreateVehicle;
      PlaceUnitXY(mortarman[i],place[(i*2)-1],place[(i*2)],false);
      PlaceUnitXY(mortarman_veh[i],vehic[(i*2)-1],vehic[(i*2)],false);
      wait(1);
      bc_type:=b_breastwork;
      bc_level:=levels[difficulty]+Rand(0,2);
      mortar_bunkers:=mortar_bunkers ^ CreateAndPlaceBuildingXYD(breas[(i*3)-2],breas[(i*3)-1],breas[(i*3)]);

      ComEnterUnit(mortarman[i],mortar_bunkers[i]);
    end;
  uc_direction:=Rand(0,5);
end;

// vytvoreni arabskeho minoveho pole
function create_minefield;
var i,xx;
begin
  mines_list:=[];
  for i=1 to (difficulty*2) do
    begin
      xx:=RandHexArea(minefield_north,false);
      PlaceMine(xx[1],xx[2],enemy,0);
      mines_list:=mines_list ^ xx;
      wait(1);
    end;
  for i=1 to difficulty do
    begin
      xx:=RandHexArea(minefield_east,false);
      PlaceMine(xx[1],xx[2],enemy,0);
      mines_list:=mines_list ^ xx;
      wait(1);
    end;
  for i=1 to (difficulty*2) do
    begin
      xx:=RandHexArea(minefield_west,false);
      PlaceMine(xx[1],xx[2],enemy,0);
      mines_list:=mines_list ^ xx;
      wait(1);
    end;
  for i=1 to difficulty do
    begin
      xx:=RandHexArea(minefield_south,false);
      PlaceMine(xx[1],xx[2],enemy,0);
      mines_list:=mines_list ^ xx;
      wait(1);
    end;
end;

// vytvareni kamikaze
function prepare_kamikaze;
var i,
    max;            // pocet kamikaze jednotek v zavislosti na obtiznosti
begin
  uc_side:=nature;
  uc_nation:=nation_nature;
  max:=2+2*difficulty;
  for i=1 to max do
    begin
      PrepareHuman(0,class_apeman_kamikaze,(-1)*(levels[difficulty]+Rand(0,1)));
      hc_agressivity:=50;
      kamikaze:=kamikaze ^ CreateHuman;
        case (i mod 2) of
      0: PlaceUnitArea(kamikaze[i],north_kamikaze,false);
      1: PlaceUnitArea(kamikaze[i],south_kamikaze,false);
        end;
      wait(1);
    end;
  uc_nation:=nation_arabian;
  uc_side:=enemy;
end;

// vytvareni flameru
function prepare_flamers;
var i,
    max;            // pocet flameru v zavislosti na obtiznosti
begin
  max:=difficulty+1;

  // priprava severni strany
  vc_control:=control_manual;
  vc_engine:=engine_combustion;
  vc_weapon:=ar_flame_thrower;
  vc_chassis:=ar_medium_trike;
  for i=1 to max do
    begin
      uc_direction:=Rand(0,5);
      PrepareHuman(sex_male,class_mechanic,(-1)*(levels[difficulty]+Rand(0,1)));
      north_flamers:=north_flamers ^ CreateHuman;
      north_flamers_veh:=north_flamers_veh ^ CreateVehicle;
      PlaceHumanInUnit(north_flamers[i],north_flamers_veh[i]);
      PlaceUnitArea(north_flamers_veh[i],north_flamers_area,false);
      wait(1);
    end;

  // priprava jizni strany
 vc_chassis:=ar_half_tracked;
  for i=1 to max do
   begin
      uc_direction:=Rand(0,5);
      PrepareHuman(sex_male,class_mechanic,(-1)*(levels[difficulty]+Rand(0,1)));
      south_flamers:=south_flamers ^ CreateHuman;
      south_flamers_veh:=south_flamers_veh ^ CreateVehicle;
      PlaceHumanInUnit(south_flamers[i],south_flamers_veh[i]);
      PlaceUnitArea(south_flamers_veh[i],south_flamers_area,false);
      wait(1);
   end;
end;

// vytvareni sniperu
function prepare_sniper;
var i,
    place,          // umisteni sniperu
    breas;          // a jejich bunkru
begin
  place:=[ 91,8, 144,76, 132,24 ];
  breas:=[ 93,13,0, 146,80,5, 129,24,2 ];
  for i=1 to 3 do
    begin
      PrepareHuman(sex_male,class_sniper,(-1)*(levels[difficulty]+Rand(0,1)));
      snipers:=snipers ^ CreateHuman;
      PlaceUnitXY(snipers[i],place[(i*2)-1],place[(i*2)],false);
      if i=3 then
        begin
          snipers_veh=snipers_veh ^ CreateVehicle;
          PlaceUnitXY(snipers_veh[1],137,28,false);
        end;
      bc_type:=b_breastwork;
      bc_level:=levels[difficulty]+Rand(0,2);
      sniper_bunkers:=sniper_bunkers ^ CreateAndPlaceBuildingXYD(breas[(i*3)-2],breas[(i*3)-1],breas[(i*3)]);
      ComEnterUnit(snipers[i],sniper_bunkers[i]);
      wait(1);
    end;
end;

// vytbareni hoover rideru
function prepare_hovers;
var i,
    max;            // pocet hooveru v zavislosti na obtiznosti
begin
  max:=difficulty+2;

  vc_chassis:=ar_hovercraft;
  vc_control:=control_manual;
  vc_engine:=engine_combustion;
  for i=1 to max do
    begin
      uc_direction:=Rand(0,5);
        case Rand(0,1) of
      0: vc_weapon:=ar_double_machine_gun;
      1: vc_weapon:=ar_multimissile_ballista;
        end;
      PrepareHuman(0,class_mechanic,(-1)*(levels[difficulty]+Rand(0,1)));
      hover_riders:=hover_riders ^ CreateHuman;
      hovercrafts:=hovercrafts ^ CreateVehicle;
      PlaceHumanInUnit(hover_riders[i],hovercrafts[i]);
      PlaceUnitArea(hovercrafts[i],hover_area,false);
      wait(1);
    end;
end;

// vytvareni laserove jednotky
function prepare_lasers;
var i,
    max;            // pocet laserovych vozide v zavislosti na obtiznosti
begin
  uc_nation:=nation_american;
  max:=1+(difficulty div 3);
  vc_control:=control_manual;
  vc_engine:=engine_combustion;
  vc_weapon:=us_laser;
  for i=1 to max do
    begin
      vc_chassis:=us_medium_wheeled+Rand(0,2);
      PrepareHuman(sex_male,class_mechanic,(-1)*(levels[difficulty]+Rand(0,1)));
      laser_riders:=laser_riders ^ CreateHuman;
      lasers:=lasers ^ CreateVehicle;
      PlaceHumanInUnit(laser_riders[i],lasers[i]);
      PlaceUnitArea(lasers[i],lasers_area,false);
      wait(1);
    end;
  uc_nation:=nation_arabian;
end;

// vytvori obyvatele arabske zakladny
function prepare_ar_base;
var i,
    bun_list,       // seznam bunkru v arabske zakladne
    class_list,     // seznam povolani krome vojaka
    max;            // pocet obyvatel arabske zakladny
begin
  bun_list:=[bun_1,bun_2,bun_3,bun_4];
  class_list:=[class_mechanic,class_engineer,class_scientistic];
  max:=difficulty*2+2;

  for i=1 to 4 do                         // arabsti bunkrovi bojovnici
    begin
      PrepareSoldier(sex_male,levels[difficulty]+1);
      origin_def:=origin_def ^ CreateHuman;
      PlaceUnitArea(origin_def[i],ar_base,false);
      ComEnterUnit(origin_def[i],bun_list[i]);
      wait(1);
    end;

  for i=1 to (max div 2) do               // arabian soldiers
    begin
      PrepareSoldier(sex_male,levels[difficulty]+Rand(0,1));
      origin_sol:=origin_sol ^ CreateHuman;
      PlaceUnitArea(origin_sol[i],ar_base,false);
      wait(1);
    end;

  for i=1 to (max div 2) do               // tvorba ostatnich povolani
    begin
      PrepareHuman(sex_male,class_list[Rand(1,3)],(-1)*(levels[difficulty]+Rand(0,1)));
      origin_others:=origin_others ^ CreateHuman;
      PlaceUnitArea(origin_others[i],ar_base,false);
      wait(1);
    end;

end;
