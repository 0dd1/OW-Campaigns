EXPORT You,Amici,Amici2,Arabi;

export everybody;

starting
begin
  // nasteveni konstant pro strany
  You=3;
  Amici=1;
  Arabi=2;
  Amici2=4;
  // uvodni inicializace
  RandomizeAll;
  debug_strings=[];
  InitBc;
  InitHc;
  InitUc;
  InitVc;

  // priprava promennych pro nastaveni obtiznosti
  SET_DIFFICULTY;

  // loadnuti savu postav + promennych
  load_postav;
  load_promenne;

  // vytvoreni arabu na zacatek
  vytvor_araby;

  // vytvoreni amiku na zacatek
  vytvor_amiky;

  // vyber, kde zacne hrac hrat
  cislo_baze=vyber_misto_pro_zakladnu;

  // placnuti nalezist podle toho, kde si vybral postaveni
  place_resource;
  
  // vyber lidi pro misi
  Ru_enter=vyber_lidi_pro_misi;
  
  // predvytvoreni 1. americkeho utoku
  create_first_utok;

  // predvytvoreni 1. americkeho utoku
  create_second_utok;

  // predvytvoreni 1. americkeho utoku
  create_third_utok;

  // create lidi pro ruskou misi
  create_rusky_konvoj(Ru_enter);

  // placni rusy do startovni zony
  placni_rusy_na_zacatek;

  InGameOff;
end;

// prohra chcipnutim burlaka
on UnitDestroyed(un) DO
begin
  if un=bur then YouLost('Burlak');
  enable;
end;

// prohra, kdyz utecou vedci
EXPORT function prohra_vedci;
begin
  YouLost('Scientists');
end;

// ukonceni mission endem z objectives
on EndTheMissionRaised(void) do
begin
  end_the_mission_allowed := false;
  konec_mise;
end;

//******************************************************
//*********  SAVENI POSTAV A PROMENNYCH  ***************
//******************************************************
// osetreni ukonceni mise
EXPORT function konec_mise;
VAR i,pom,pom1;
VAR drv;
VAR all_people;
VAR plab;
begin
  DisableExclamations;
  gained_medals:=[];
  missing_medals:=[];

  if IsOK(masha) and GetSide(masha)=You then AddMedal('Masha',1)
                                        else AddMedal('Masha',-1);

  plab=0;
  for i in [Am_lab1,Am_lab2,Am_lab3] do
    if GetSide(i)=You then plab=plab+1;
  if plab>=3 then AddMedal('Capture',1)
             else AddMedal('Capture',-1);
   
  if GetTech(tech_TauRad,You)=state_researched or GetTech(tech_SpacAnom,You)=state_researched
    then AddMedal('Invention',1)
    else AddMedal('Invention',-1);

  GiveMedals('Main');

  // ostatni vojaci
  pom=FilterAllUnits([[f_side,You],[f_type,unit_human]]);
  pom1=FilterAllUnits([[f_side,You],[f_type,unit_vehicle]]);
  for i in pom1 do
    if IsDrivenBy(i) then
      begin
        drv=IsDrivenBy(i);
        if not (drv in pom) then pom=pom^drv;
      end;
  all_people=pom;

  all_people=all_people diff UnitFilter(all_people,[f_or,[f_class,class_apeman],[f_class,class_apeman_engineer],[f_class,class_apeman_soldier]]);

  RewardPeople(all_people);

  SaveCharacters(bur,'Burlak');
  SaveCharacters(dol,'Dolgov');
  SaveCharacters(kapisto,'Kapitsova');
  SaveCharacters(petro,'Petrosyan');
  SaveCharacters(pop,'Popov');
  SaveCharacters(bel,'Belkov');
  SaveCharacters(gny,'Gnyevko');
  SaveCharacters(kiri,'Kirilenkova');
  SaveCharacters(koval,'Kovalyuk');
  SaveCharacters(schol,'Scholtze');
  SaveCharacters(kuzmov,'Kuzmov');
  SaveCharacters(karam,'Karamazov');
  SaveCharacters(petrov,'Petrovova');
  SaveCharacters(gleb,'Gleb');
  SaveCharacters(lipsch,'Lipschin');
  SaveCharacters(tito,'Titov');
  SaveCharacters(xavier,'Xavier');
  SaveCharacters(obluk,'Oblukov');
  SaveCharacters(kozlov,'Kozlov');


  if plab>=2 then
    SaveVariable(true,'LabCaptured')
  else
    SaveVariable(false,'LabCaptured');

  if not IsOK(masha) then
    DeleteVariable('Masha');

  SaveVariable(GetTech(tech_TauRad,You),'shifting');
  SaveVariable(GetTech(tech_SpacAnom,You),'slowing');

  pom:=[];
  for i in everybody do
    if GetLives(i) > 0 then
      pom:=pom ^ i;
  pom:=pom diff [bur,dol,kapisto,petro,pop,bel,gny,kiri,koval,schol,kuzmov,karam,petrov,gleb,lipsch,tito,xavier,obluk,kozlov,0];
  SaveCharacters(pom,'survivors3');

  YouWin();
  EnableExclamations;
  exit;
end;

//*******************************************************
//*********  LOADENI POSTAV A PROMENNYCH  ***************
//*******************************************************
function load_postav;
begin
  pop=nacti_osobu('Popov');
  SetSide(pop,You);
  dol=nacti_osobu('Dolgov');
  bur=nacti_osobu('Burlak');
  kapisto=nacti_osobu('Kapitsova');
  petro=nacti_osobu('Petrosyan');
  bel=nacti_osobu('Belkov');
  gny=nacti_osobu('Gnyevko');
  kiri=nacti_osobu('Kirilenkova');
  koval=nacti_osobu('Kovalyuk');
  schol=nacti_osobu('Scholtze');
  kuzmov=nacti_osobu('Kuzmov');
  karam=nacti_osobu('Karamazov');
  petrov=nacti_osobu('Petrovova');
  gleb=nacti_osobu('Gleb');
  lipsch=nacti_osobu('Lipschin');
  tito=nacti_osobu('Titov');
  xavier=nacti_osobu('Xavier');
  obluk=nacti_osobu('Oblukov');
  kozlov=nacti_osobu('Kozlov');
end;

function load_promenne;
begin
  masha_l=LoadVariable('Masha',[ru_heavy_tracked,engine_combustion,control_manual,ru_heavy_gun]);

  maccepted=LoadVariable('MercHandedover',true);    // prijal gensherovu nabidku
  mhandedover=not LoadVariable('GenscherBetrayed',false); // true - kdyz ji genscherovi vydal
end;