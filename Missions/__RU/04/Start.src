var
  ident,
  peopledef,
  un,
  whole_units;

export
  Burlak_saved,
  Gaydar_saved,
  Gaydar_wait,

  you,
  ally,
  arab,
  attack,
  selfdefend,
  sm_attack1,
  sm_attack2,
  sm_attack3,
  other,
  alliance,

  Burlak,
  Gnyevko,
  Platonov,
  Belkov,
  Scholtze,
  Kuzmov,
  Kovalyuk,
  SoldierX,
  Soldier1,
  Scientist1,
  Engineer1,
  Mechanic1,
  Popov,
  Gaydar,
  Heike,
  new,

  player_units,
  saved_player_units,

  Convoy,
  Convoy_length,
  Convoy_delay,
  FirstCargo,
  trucks_diverted_count,
  trucks_diverted_list,
  number_of_trucks,
  number_of_guns,
  number_of_hv_guns,
  number_of_gatlings,
  number_of_hv_mchn_gun,

  konec,

  RecklessAttack,
  BelkovFreed,

  pocet,
  count,
  attack_offset,
  attack_count,
  ar_killed,
  was_combat,
  driver,
  lab_upgraded,
  workshop_upgraded,
  depot_build,
  workshop_build,
  armoury_build,
  bunker_build,
  turret_build,
  base_dismantled,

  player_building_captured,
  pocet_budov,

  waypointsEND,
//  selfdefendEND,

  sm_pocet1_2,
  sm_pocet3,

  Disabled4,
  Disabled8,
  Disabled9,
  Disabled13;


function set_difficulty;
begin
  number_of_trucks:=[20,17,14][difficulty];
  number_of_guns:=[13,11,9][difficulty];
  number_of_hv_guns:=[6,5,4][difficulty];
  number_of_gatlings:=[8,6,4][difficulty];
  number_of_hv_mchn_gun:=[3,2,1][difficulty];

  convoy_delay:=[[60,120],[45,105],[30,90]][difficulty];

  attack_offset:=[Rand(1$0,1$30),Rand(0$30,1$0),Rand(0$0,0$30)][difficulty];
  attack_count:=[3,5,7][difficulty];
  Gaydar_wait:=[1$30,1$10,0$45][difficulty];

  waypointsEND:=[
  [[103,105],[76,36],
  [111,105],[79,94],
  [118,104],[71,85],
  [122,98],[62,75],
  [99,48],[54,52],
  [107,55],[59,38]],

  [[91,102],[103,105],
  [111,105],[79,94],
  [118,104],[71,85],
  [122,98],[62,75],
  [120,76],[54,52],
  [107,55],[59,38],
  [99,48],[66,35],
  [88,42],[76,36]],

  [[91,102],[103,105],
  [111,105],[79,94],
  [118,104],[71,85],
  [122,98],[62,75],
  [122,89],[56,62],
  [120,76],[54,52],
  [113,62],[55,44],
  [107,55],[59,38],
  [99,48],[66,35],
  [88,42],[76,36]]]
  [difficulty];

{  selfdefendEND:=[
  [[89,16],
  [79,22],
  [71,28],
  [63,32],
  [55,38],
  [50,44],
  [52,59],
  [62,77],
  [72,99],
  [73,112]],

  [[89,16],
  [79,22],
  [71,28],
  [63,32],
  [55,38],
  [50,44],
  [52,59],
  [62,77],
  [72,99],
  [73,112]],

  [[89,16],
  [79,22],
  [71,28],
  [63,32],
  [55,38],
  [50,44],
  [52,59],
  [62,77],
  [72,99],
  [73,112]]]
  [difficulty];}
end;

function read_parameters;
var i;
begin
  you:=3;
  ally:=6;
  arab:=2;

  ChangeSideFog(you,ally);

  SetAttitude(ally,you,att_friend,true);
  SetAttitude(ally,arab,att_enemy,true);
  SetAttitude(you,arab,att_enemy,true);

  trucks_diverted_count:=0;
  trucks_diverted_list:=[];

  Disabled4:=false;
  Disabled8:=false;
  Disabled9:=false;

  sm_pocet1_2:=0;
  sm_pocet3:=0;
  pocet:=0;
  konec:=false;

  BelkovFreed:=LoadVariable('belkov_saved',false);
  case BelkovFreed of
    0:BelkovFreed:=true;
    1:BelkovFreed:=false;
  end;

  RecklessAttack:=LoadVariable('stav_utoku',true);
  case RecklessAttack of
    1:RecklessAttack:=false;
    2:RecklessAttack:=false;
    3:RecklessAttack:=true;
  end;

  lab_upgraded:=false;
  workshop_upgraded:=false;
  depot_build:=false;

  player_building_captured:=false;

  pocet_budov:=0;

  Burlak_saved:=false;
  Gaydar_saved:=false;
end;                                             

function nacti_osobu(ident);
begin
  if TestCharacters(ident) then
    if CheckCharacterSet(ident)=1 then
      result:=CreateCharacter(ident)
    else
      result:=CreateCharacterSet(ident)
  else
    result:=0;
end;

function create_people;
var def,i,x,y,fff;
begin
  other:=[];

  uc_side:=arab;
  uc_nation:=nation_arabian;

  Heike:=NewCharacter('Heike');

//******************************************************

  uc_side:=ally;
  uc_nation:=nation_russian;

  Gaydar:=NewCharacter('Gaydar');
  Kuzmov:=NewCharacter('Kuzmov');

//******************************************************

  uc_side:=you;
  uc_nation:=nation_russian;

  Burlak:=nacti_osobu('Burlak');
  Gnyevko:=nacti_osobu('Gnyevko');
  Belkov:=nacti_osobu('Belkov');
  Scholtze:=nacti_osobu('Scholtze');
  Kovalyuk:=nacti_osobu('Kovalyuk');

  Popov:=NewCharacter('Popov');
  Platonov:=NewCharacter('Platonov');

//******************************************************

  other:=nacti_osobu('survivors1');

//******************************************************

  PrepareHuman(sex_male,class_soldier,2);
  hc_name:='';
  Soldier1:=CreateHuman;

  PrepareHuman(sex_male,class_soldier,3);
  hc_name:='';
  SoldierX:=CreateHuman;

  PrepareHuman(sex_male,class_engineer,3);
  hc_name:='';
  Scientist1:=CreateHuman;

  PrepareHuman(sex_male,class_engineer,2);
  hc_name:='';
  Engineer1:=CreateHuman;

  PrepareHuman(sex_male,class_mechanic,3);
  hc_name:='';
  Mechanic1:=CreateHuman;

  fff:=([Burlak,Gnyevko,Belkov,Scholtze,Kuzmov,Kovalyuk,Soldier1,Scientist1,Engineer1,Mechanic1] union other) diff [0];
  if fff<14 then
    for i:=1 to 14-fff do
    begin
      case Rand(1,3) of
        1:PrepareHuman(0,class_mechanic,2);
        2:PrepareHuman(0,class_engineer,2);
        3:PrepareHuman(0,class_soldier,2);
      end;
      hc_name:='';
      new:=new^CreateHuman;
    end;

  for un in (([Popov,Burlak,Gnyevko,Belkov,Scholtze,Kovalyuk,Soldier1,Scientist1,Engineer1,Mechanic1] union other) union new) diff [0] do
    PlaceUnitXYR(un,102,3,7,false);

  PlaceUnitXYR(Kuzmov,92,75,10,false);
end;

function make_alliance;
var i,pom;
begin
  whole_units:=(([Popov,Burlak,Gnyevko,Belkov,Scholtze,Kovalyuk,Soldier1,Scientist1,Engineer1,Mechanic1] union other) union new) diff [0]; // without Kuzmov
  alliance:=whole_units diff player_units;
  SetSide(alliance,ally);
end;

function alliance_go_away;
var points,waypoints;
begin
  waypoints:=[[98,6],[94,9],[90,14],[87,17],[81,18],[80,23],
  [86,34],[96,45],[108,56],[113,62],[119,73],[121,79],[122,89],[123,98],[122,108],
  [115,113],[91,114],[73,113],
  [71,115],[71,129],[73,138]];

  for points in waypoints do
      AddComMoveXY(alliance,points[1],points[2]);
end;

function select_people;
var units;
begin
  units:=[sel_hired,sel_not_changeable,sel_change_class,Burlak]^
         [sel_not_hired,sel_not_changeable,sel_dont_change_class,Popov]^
         [sel_not_hired,sel_changeable,sel_change_class,Scholtze,Gnyevko,Belkov,Kovalyuk,Soldier1,Scientist1,Engineer1,Mechanic1]^new^other;
  player_units:=[Burlak,Kuzmov] union CharacterSelection('Select 8 characters to go with you',8,8,units,[class_soldier, class_mechanic, [class_engineer,1], class_scientistic]);
end;

export function create_gaydar;
begin
  PlaceUnitXYR(Gaydar,17,1,3,false);
  ComMoveXY(Gaydar,23,10);
  enable(15);
  Dialogue10;
  repeat
    Wait(0$1);
    if GetSide(Gaydar) = you then
         exit;

  until Attacks(Gaydar)=0;
  ComMoveXY(Gaydar,27,16);
  Wait(0$30);
  repeat
    Wait(0$1);
    if GetSide(Gaydar) = you then
         exit;

  until Attacks(Gaydar)=0;
  ComMoveXY(Gaydar,33,30);
  Wait(0$30);
  repeat
    Wait(0$1);
    if GetSide(Gaydar) = you then
         exit;

  until Attacks(Gaydar)=0;
  ComMoveXY(Gaydar,27,47);
end;

starting
begin
  FogOff(false);

  InitBc;
  InitHc;
  InitUc;
  InitVc;

  IngameOn;

  disable(2);
  disable(3);
  disable(4);
  disable(5);
  disable(6);
  disable(7);
  disable(8);
  disable(9);
  disable(10); // placing of arabian attack
  disable(11); // placing of small arabian attack
  disable(12);
  disable(13);
  disable(14);
  disable(15);
  disable(16);
  disable(17);
  disable(18); // convoy without task controling
  disable(19); // Mission Objective - Dismantling Base

  Randomize;
  RandomizeAll;

  set_difficulty;
  read_parameters;

  prepare_convoy;
  prepare_arabian_attack;
  prepare_small_arabian_attack;

  create_people;

  CenterNowOnXY(102,3);

  Wait(0$2);

  Dialogue1; // After start of the mission

  select_people;

  ChangeMissionObjectives('M1');  // Objective change 1 (+Build, +Defend)

  make_alliance;

  SaveForQuickRestart;  // Save for Quick Restart

  IngameOff;

  alliance_go_away;
end;

export function konec_mise;
VAR pom,pom1,i,all_people,drv,filter;
begin                                        
  Wait(0$1);
  gained_medals:=[];
  missing_medals:=[];

  saved_player_units:=UnitFilter(saved_player_units,[f_not,[f_or,[f_class,class_apeman_engineer],[f_class,class_apeman_soldier]]]);

  if (0+saved_player_units) >= 9 then
    AddMedal('LittleLosses',1)
  else AddMedal('LittleLosses',-1);

  if ar_killed >= waypointsEND-3 then
    AddMedal('EnemyLosses',1)                  
  else AddMedal('EnemyLosses',-1);

  filter:=FilterAllUnits([[f_type,unit_building],[f_not,[f_or,[f_btype,b_oil_power],[f_btype,b_oil_mine],[f_btype,b_bunker]]]]);

  if filter=0 then
    AddMedal('Dismantle',1)
  else AddMedal('Dismantle',-1);

  GiveMedals('Main');

  RewardPeople(saved_player_units);

  SaveCharacters(Burlak,'Burlak');
  SaveCharacters(Gnyevko,'Gnyevko');
  SaveCharacters(Kovalyuk,'Kovalyuk');
  SaveCharacters(Belkov,'Belkov');
  SaveCharacters(Scholtze,'Scholtze');
  SaveCharacters(Kuzmov,'Kuzmov');
  SaveCharacters(Gaydar,'Gaydar');

  SaveCharacters((other union new) diff [Burlak,Belkov,Gnyevko,Platonov,Kovalyuk,Popov,Scholtze,Kuzmov,Gaydar],'survivors1');

  YouWin;
  exit;
end;












