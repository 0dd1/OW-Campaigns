
//rusaci se vraceji
var rusove_mimo;
var pom_rumi;
function rusove_mimo_bazi;
begin
  pom_rumi=rusove diff filterunitsinarea(rubase0,[[f_type,unit_human],[f_ok]]);
  rusove_mimo=pom_rumi diff filterunitsinarea(rubase0,[[f_type,unit_vehicle],[f_ok]]);
  result=rusove_mimo;
end;

every 0$1 trigger rusove_mimo_bazi do
begin
  if retreat then exit;
  comreturn(rusove_mimo);
  enable;
end;

var healeri_mimo;
function healeri_mimo_rb;
begin
  healeri_mimo=healeri diff filterunitsinarea(repairbay,[[f_type,unit_human],[f_ok]]);
  result=healeri_mimo;
end;

//healeri mimo repairbay
every 0$0.9 trigger healeri_mimo_rb do
begin
  if retreat then exit;
  commovexy(healeri_mimo,85,97);
  enable;
end;


//poskozeni rusaci sa vraceji a opravuji se
var poskoz_;
var kn_;
every 0$0.7 do
begin
  if retreat then exit;
  poskoz_=unitfilter(rusove0,[[f_type,unit_vehicle]]);
  for kn_ in poskoz_ do begin
    if getlives(kn_)<=hr_auta then begin
      rusove0=rusove0 diff kn_;
      poskoz=poskoz^[kn_];
    end;
  end;
  poskoz_=unitfilter(rusove0,[[f_type,unit_human]]);
  for kn_ in poskoz_ do begin
    if getlives(kn_)<=hr_lidi then begin
      rusove0=rusove0 diff kn_;
      poskoz=poskoz^[kn_];
    end;
  end;
  commovexy(poskoz,80,96);
  enable;
end;

var pom_rupo;
var driver;
var kn__;
every 0$1.5 do
var p95,p96,poskpom;
begin
  if retreat then exit;
  poskpom=poskoz;
  poskoz=[];
  for p95 in poskpom do
    if islive(p95) then poskoz=poskoz^[p95];

  pom_rupo=prebytek isect filterunitsinarea(repairbay,[[f_side,rus],[f_type,unit_vehicle]]);
  if (0+prebytek)>mpard then begin
    if (0+prebytek-mpard)>=(0+pom_rupo) then p96=(0+pom_rupo) else p96=(0+prebytek-mpard);
    for p95=1 to p96 do destroyunit(pom_rupo[p95]);
  end;

  pom_rupo=filterunitsinarea(repairbay,[[f_side,rus],[f_type,unit_vehicle]]) isect poskoz;
  for kn__ in pom_rupo do begin
    driver=isdrivenby(kn__);
    comhold(driver);
    setfuel(kn__,100);
    comexitvehicle(kn__);
    wait(0$0.5);
    addcomrepairvehicle([driver],kn__);
    addcommoveunit([driver],kn__);
  //   addcommovexy(driver,35,33);
    addcomfree(driver);
    poskoz=poskoz diff [kn__];
     //repaired=repaired^[kn__];
    rusove0=rusove0^[kn__];
  end;
  pom_rupo=filterunitsinarea(repairbay,[[f_side,rus],[f_type,unit_human]]) isect poskoz;
  for kn__ in pom_rupo do begin
    poskoz=poskoz diff [kn__];
    pacienti=pacienti^[kn__];
   end;
  for kn__ in pacienti do begin
    if getlives(kn__)>=950 then begin
      pacienti=pacienti diff [kn__];
      rusove0=rusove0^[kn__];
    end;
  end;
  if pacienti>=1 then comheal(healeri,pacienti[1]);
  enable;
end;

//bacha na vozitka co se prebarvi
on vehiclecaptured(newid,oldid,origside,cptrdby) do
if origside=rus then begin
  rusove0=rusove0 diff [oldid];
  poskoz=poskoz diff [oldid];
end;






